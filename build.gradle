buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id "com.jfrog.bintray" version "1.3.1"
}

apply plugin: 'java'

if (new File("./gradle/localrepo.gradle").exists()) {
    apply from: "./gradle/localrepo.gradle"
}

group = "com.burgstaller"
archivesBaseName = 'okhttp-digest'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenLocal()
    jcenter()
}

ext {
    okhttpVersion = "3.12.6"
}


dependencies {
    implementation "com.squareup.okhttp3:okhttp:${okhttpVersion}"
    testImplementation "com.squareup.okhttp3:logging-interceptor:${okhttpVersion}"
    testImplementation "com.squareup.okhttp3:mockwebserver:${okhttpVersion}"
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.assertj:assertj-core:3.9.1'
    testImplementation 'org.mockito:mockito-core:2.15.0'
}

def siteUrl = 'https://github.com/rburgst/okhttp-digest'
def gitUrl = 'https://github.com/rburgst/okhttp-digest.git'

if (!hasProperty('bintray_user')) {
    project.ext {
        bintray_user = "undef"
        bintray_api_key = "undef"
    }
}

bintray {
    user = property('bintray_user')
    key = property('bintray_api_key')

    configurations = ['archives'] //When uploading configuration files
    pkg {
        repo = 'android'
        name = project.name
        desc = 'digest authenticator for okhttp'
        websiteUrl = siteUrl
        issueTrackerUrl = 'https://github.com/rburgst/okhttp-digest/issues'
        vcsUrl = gitUrl
        licenses = ['Apache-2.0']
        labels = ['android', 'okhttp']
        publicDownloadNumbers = true
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

task findConventions {
    doLast {
        println project.getConvention()
    }
}

